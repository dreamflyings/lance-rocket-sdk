OUTPUT_ARCH("riscv")
ENTRY(_start)

MEMORY
{
	ramall(rwx)	: o = 0x80000000, l = 0x8000 /* 32KB */
	ram(rwx)	: o = 0x80000100, l = 0x7f00 /* ELFヘッダが0x80000000からロードされるので避けている */
	userstack(rw)	: o = 0x80007500, l = 0x0000 /* ユーザー・スタック */
	bootstack(rw)	: o = 0x80008000, l = 0x0000 /* 起動処理用スタック(end of RAM) */
	intrstack(rw)	: o = 0x80008000, l = 0x0000 /* 割込み処理用スタック(end of RAM) */
}

SECTIONS
{
	. = 0x80000100;		/* 先頭番地 */

	.text : {
	      	text_start = .;
	      	*(.text.init)
	      	*(.text)
		etext = .;
	} > ram

	.rodata : {
		rodata_start = .;
		*(.strings)
		*(.rodata)
		*(.rodata.*)
		erodata = .;
	} > ram

	.data : {
	      	data_start = .;
	      	*(.data)
		edata = .;
	} > ram

	.sdata : {
	      	sdata_start = .;
	      	*(.sdata)
		esdata = .;
	} > ram

	.bss : {
	       bss_start = .;
	       *(.bss)
	       *(COMMON)
	       ebss = .;
	} > ram
	
	.sbss : {
	       sbss_start = .;
	       *(.sbss)
	       *(COMMON)
	       esbss = .;
	} > ram

	. = ALIGN(4);
	_end = .;

	.userstack : {
	       userstack = .;
	} > userstack

	.bootstack : {
	       bootstack = .;
	} > bootstack

	.intrstack : {
	       intrstack = .;
	} > intrstack
}
